# 2023.6: Answering Questions, Testing, Wheats

###### tags: `Tag(sp23)`

<!-- 2023: too long, didn't do much Q&A -->

## Logistics

**There is a Forge update coming this morning.** You should install it when it arrives---it contains a fix for an issue some of you noticed with `example`. The issue could potentially result in a strange error message, or in examples passing when they shouldn't.

**Fill out the GPT-3 form, even if you don't plan to use it in 1710.** It's important that everybody agrees to the terms (which include opting out) to avoid confusion or attempts to exploit the system. If you have concerns about this, please talk to me and I'll do my best to address the problem.

## Forge Logging

Forge is a tool we develop here. The work on Forge has largely been done by undergrads, supplemented by bits of Tim's time and contributions by Ben, Siddhartha, and others. So we're very interested in detecting problems early. We're also interested in the ways that you all use Forge, spotting places I might explain things poorly in class or how the tool's errors might guide you to make fixes. 

So we'd like to ask you to help us by _logging_ your work in Forge. This is opt-in, but doing so is lightweight. You just add some additional info to the `#lang` part of your file. Just after the language name, you add an assignment ID and your anonymous Gradescope ID. E.g., to turn logging in for this class, if I wasn't concerned about being anonymous, I'd write (remember the quotes):

```forge
#lang forge/bsl "ttt_class" "tim_nelson@brown.edu"
```

This will log changes I make to the model every time I run it, and store a record of those changes in the cloud. This is why using Gradescope's anonymous grading isn't good enough. We need:
* to be able to contact someone if they've opted into logging and we spot an issue; but also
* not give away your identity in grading! 

Again, participating in logging in opt-in, but we hope you'll help us by enabling it on your Forge homeworks.


## Tests In Forge

Since all Forge ever does is check satisfiability, a test is a satisfiability check---just like a `run` statement. The difference is that a test doesn't open a visualizer, or wait for you to say to move on.

We covered `example`s very quickly last time, but today we'll do three more things related to testing:
* talk more about example syntax;
* introduce test suites; and
* introduce how to check whether a _property_ holds of a predicate. 

### Test Suites

You can use `test suite for` to group your tests and examples according to the predicate you're testing. For example, in the example from last time:

```alloy
test suite for allWellformed {
    example middleRowWellFormed is {allWellformed} for {
        X = `X0
        O = `O0
        Player = `X0 + `O0
        Board = `Board0
        board = `Board0 -> (3 -> 0 -> `X0 +
                            1 -> 1 -> `X0 + 
                            1 -> 2 -> `X0)
    }
}
```

This is convenient when you have many tests; it also helps to evaluate what you're testing. We ask you to use `test suite for` for your tests on Forge 1. Note that you can also include _negative_ examples, like this:

```alloy
    example middleRowNotWellFormed is {not allWellformed} for {
        X = `X0
        O = `O0
        Player = `X0 + `O0
        Board = `Board0
        board = `Board0 -> (3 -> 0 -> `X0 +
                            1 -> 1 -> `X0 + 
                            1 -> 2 -> `X0)
    }

```

Why would you want to do this? Think about what kinds of behavior you might want to check about your Forge models. Setting aside modeling errors like not having the right fields in a `sig`, bugs tend to involve either:
* getting an instance you didn't expect (the model's constraints are too permissive); or 
* _not_ getting an instance you expected (the model's constraints are too restrictive).

We'll sometimes refer to these as "underconstraint" and "overconstraint". To see why it's important to check in both directions, imagining you were writing unit tests for a method that returned `boolean`. If you only test `true`, the method that returns `false` would pass all your tests.

### Checking Properties as Tests

Examples aren't expressive enough to check that a property always holds of a predicate. For example, I might want to check whether the `traces` predicate _always guarantees_ wellformedness. To check that, I can write an _assertion_:

```alloy
assert allWellformed is necessary for traces 
for exactly 10 Board, 3 Int
for {next is linear}
```

A few important things to notice:
* An `assert` is very like a `run`, except that the visualizer won't open unless the assertion fails. 
* Assertions, like runs, can take _numeric_ bounds as well as annotations (`next is linear`). The numeric bounds always come before the annotations.
* `A` is necessary for `B` means that any time `B` is true, `A` must hold. So here we're saying that wellformedness is a consequence of the way we wrote `traces`. 

Unfortunately this assertion doesn't pass: we never actually constrained moves to be at valid indexes. If we go back to the model and change that by adding some guard constraints to `move`:

```alloy
    -- Added Feb 6:
    row >= 0 and row <= 2 
    col >= 0 and col <= 2
```

the assertion begins to pass. We have just used Forge to confirm that this version of the rules makes it impossible to reach a board state that isn't well formed. There are caveats, as with all things! 
* What if we made a modeling error? (That's why we test our models.)
* What if the bounds aren't high enough to find a counterexample? (Here, we know the bounds are sufficient because we can represent any board state, and games never go beyond 9 steps.)

You can also write an assertion that works in reverse, checking _sufficiency_ rather than _necessity_; we'll get to that next week.

#### Performance 

That last check took a while---40 seconds on my laptop! Why is that? 

One reason is that, often, unsatisfiability takes longer than finding a single satisfying instance. If there are satisfying instances, the search can be "lucky" without exhaustively exploring the entire space. Forge doesn't actually examine each possible instance; it does something more intelligent and efficient. But a full exploration is still usually more costly. 

Another is that modeling choices and the nature of what we're checking matters. I lowered the `Int` bound to 3 bits because 4 didn't add anything, and it would have greatly slowed down the check. There are faster ways of running this check, which we'll get to. (Consider: do we really need all 10 board states around to conclude that wellformedness is preserved by the system?)

### Example Syntax

The syntax of examples is a bit different from writing constraints. The reason is that examples are actually all annotations to the solver, kind of like `next is linear` was. At the moment, examples won't "understand" ordinary Forge syntax. When we're saying what a certain field contains in an example, we need to give it all in one equality. E.g., I write:

```alloy
        board = `Board0 -> (1 -> 0 -> `X0 +
                            1 -> 1 -> `X0 + 
                            1 -> 2 -> `X0)

```

to say that the `board` field contains these values. If there were multiple boards, I'd have to include those as well, like this:

```alloy
example twoBoards is {allWellformed} for {
        X = `X0
        O = `O0
        Player = `X0 + `O0
        Board = `Board0 + `Board1
        board = `Board0 -> (1 -> 0 -> `X0 +
                            1 -> 1 -> `X0 + 
                            1 -> 2 -> `X0) 
                    +
                `Board1 -> (0 -> 1 -> `X0 +
                            1 -> 1 -> `X0 + 
                            2 -> 1 -> `X0)
}
```

Note well the `+` between the blocks for the different boards. In an example, we give the _entire contents_ of the field, across all objects that have the field. 

### Optional: Other Kinds of Tests

We'll talk more about testing this week and next. The above should suffice for homework, etc. But for completeness, there's another kind of test you can write:

You can write blocks of general tests wrapped in the `test expect` operator, and name individual tests before their constraint block. E.g., something that's a really good idea to include with a trace-based model is a test that there are any traces whatsoever:

```alloy
test expect {
    tracesExist: {wellformed and traces} is sat
}
```

This is more general, but less intentional: the blocks being tested can get hard to read, and so we encourage you to use `assert ... is necessary for ...` whenever you're able.

## Wheats

We discussed how to query the good-implementation (called "wheat" or "oracle") that we give you with the stencil. **See the lecture capture for more info.**

## Frequent Questions

### Nulls

Suppose I added this predicate to our `run` command:

```alloy
pred myIdea {
    all row1, col1, row2, col2: Int | 
        (row1 != row2 or col1 != col2) implies
            Trace.initialState.board[row1][col1] != 
            Trace.initialState.board[row2][col2]
}
```

For context, recall that we had defined a `Trace` sig earlier:

```alloy
one sig Trace {
  initialState: one State,
  nextState: pfunc State
}
```

What do you think would happen?

<details>
<summary>Think (or try it in Forge) then click!</summary>
It's very likely this predicate would be unsatisfiable, given the constraints on the initial state. Why? 
    
Because null equals itself. In Forge, null is called `none`. We can check this:
    
```alloy
    test expect {
        nullity: {none != none} is unsat
    } 
```    
    
Thus, when you're writing constraints like the above, you need to watch out for `none`: _every_ cell in the initial board is equal to _every_ other cell!
</details>

## Some Versus Some

The keyword `some` is used in 2 different ways in Forge:
* it's a _quantifier_, as in `some s: State, some p: Player | winner[s, p]`; and
* it's a _constraint operator_, as in `some Traces.initialState.board[1][1]`. 

We kept the same syntax as Alloy on this for backward compatability. 

## Implies vs. Such That

You can read `some row : Int | ...` as "There exists some integer `row` such that ...". The transliteration isn't quite as nice for `all`; it's better to read `all row : Int | ...` as "In all integer `row`s, it holds that ...". 

If you want to _further restrict_ the values used in an `all`, you'd use `implies`. But if you want to _add additional requirements_ for a `some`, you'd use `and`.  Here are 2 examples:
* "Everybody who has a `parent1` doesn't also have that person as their `parent2`": `all p: Person | some p.parent1 implies p.parent1 != p.parent2`.
* "There exists someone who has a `parent1` and a `spouse`": `some p: Person | some p.parent1 and some p.spouse`.

**Technical aside:** The type designation on the variable can be interpreted as having a character similar to these add-ons: `and` (for `some`) and `implies` (for `all`). E.g., "there exists some `row` such that `row` is an integer and ...", or "In all `row`s, if `row` is an integer, it holds that...".

## There Exists `some` Object vs. Some Instance

Forge searches for instances that satisfy the constraints you give it. Every `run` in Forge is about _satisfiability_; answering the question "Does there exist an instance, such that...". 

Crucially, you cannot write a Forge constraint that quantifies over _instances_ themselves. You can ask Forge "does there exist an instance such that...", which is pretty flexible on its own. E.g., if you want to check that something holds of _all_ instances, you can ask Forge to find counterexamples. This is how `assert ... is necessary for ...` is implemented, and how the examples from last week worked.

## One Versus Some

The `one` quantifier is for saying "there exists a UNIQUE ...". As a result, there are hidden constraints embedded into its use. `one x: A | myPred[x]` really means, roughly, `some x: A | myPred[x] and all x2: A | not myPred[x]`. This means that interleaving `one` with other quantifiers can be subtle; I try not to use it in class for that reason.

...but it is so convenient sometimes...


<!--
// NO TIME FOR THIS 

## Testing Predicate Equivalence

Checking whether or not two predicates are _equivalent_ is the core of quite a few Forge applications---and a great debugging technique sometimes. 

How do you do it? Like this (note one test is correct, one incorrect):

```alloy
pred myPred1 {
    some i1, i2: Int | i1 = i2
}
pred myPred2 {
    not all i2, i1: Int | i1 != i2
}
test expect {
    -- correct: "no counterexample exists"
    p1eqp2_A: {
        not (myPred1 iff myPred2)        
    } is unsat
    -- incorrect: "it's possible to satisfy what i think always holds"
    p1eqp2_B: {
        myPred1 iff myPred2
    } is sat

}
```

If you get an instance where the two predicates aren't equivalent, you can use the Sterling evaluator to find out **why**. Try different subexpressions, discover which is producing an unexpected result!



// NO TIME FOR THIS MONDAY FEB 6

## Back To Tic-Tac-Toe: Ending Games

When we stopped last time, we'd written this `run` command:

```alloy
run {
  wellformed
  traces
} for exactly 10 State for {next is linear}
```

**Reminder:** Without a `run`, a `test`, or a similar _command_, running a Forge model will do nothing. 

From this `run` command, Forge will find _traces_ of the system (here, games of Tic-Tac-Toe) represented as a linear sequence of exactly 10 `State` objects.

Do you have any worries about the way this is set up?

<details>
<summary>Think, then click!</summary>
Are all Tic-Tac-Toe games 10 states long? 
    
Well, _maybe_; it depends on how we define a game. If we want a game to stop as soon as nobody can win, our `exactly 10 State` bound is going to prevent us from finding games that are won before the final cell of the board is filled.    
</details>

Let's add the following guard constraint to the `move` transition, which forces games to end as soon as somebody wins.

```alloy
all p: Player | not winner[pre, p]
```

Now we've got problems. Normally we could fix the issue by getting rid of the `exactly`. Unfortunately, there's a hidden snag: when we tell Forge in this way that that `next` is linear, it automatically makes the bound exact. 

This behavior, which I admit is bizarre at first, exists for two reasons:
* historical reasons: we inherit `is linear` from Alloy, which uses somewhat different syntax to mean the same thing; and
* performance reasons: since the `is linear` annotation is almost always used for trace-generation, and trace-generation solving time grows (in the worst case) exponentially in the length of the trace, we will almost always want to reduce unnecessary uncertainty. Forcing the trace length to always be the same reduces the load on the solver, and makes trace-generation somewhat more efficient.

But now we need to work around that constraint. Any ideas? Hint: do we need to have _only one_ kind of transition in our system?
 
<details>
<summary>Think, then click!</summary>
No. A common way to allow trace length to vary is by adding a "do nothing" transition. (In the literature, this is called a _stutter transition_.) 
    
The trick is in how to add it without also allowing a "game" to consist of nobody doing anything. To do that requires some more careful modeling.

</details>


-->